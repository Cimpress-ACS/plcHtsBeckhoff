<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.23">
  <POU Name="FB_DtChnToLine_JobDone" Id="{71232ca6-e241-4f50-a926-190534bba615}">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DtChnToLine_JobDone
VAR_INPUT
	bolResetError: BOOL;
	pSIf: POINTER TO T_Ctrl_SIf_DPJ;
END_VAR
VAR_OUTPUT
	///Error value
	intError: INT;
END_VAR
VAR
	/// Internal Variable
	intStatemachine: INT;
	/// Data not correct
	bolDataBad: BOOL;
	/// Retry Counts for error message
	intCountRetry: INT;
	/// Buffer for Senddata
	aBuffer: ARRAY[0..cDtChnBuffer] OF T_JobDone_DtChn;
	/// Pointer for new Input
	intPBufferInput: INT;
	/// Pointer for readout
	intPBufferOutput: INT;
	/// Data Channel Specific Variable
	intIndexOldest: INT;
	/// Date and time of the oldes imput
	dtOldestTag: DT;
END_VAR
VAR CONSTANT
	cDtChnBuffer: INT := 20;
	cSTA_SUB_Data_Init: INT := 900;
	cSTA_SUB_Data_WaitForJob: INT := 902;
	cSTA_SUB_Data_SendData: INT := 904;
	cSTA_SUB_Data_Acknowledge: INT := 906;
	cSTA_SUB_Data_Retry: INT := 908;
	cSTA_SUB_Data_Error: INT := 910;
	cDataState_DONE: INT := -1;
	cDataState_Ready: INT := 1;
	cDataState_Retry: INT := -99;
	cDataState_Error: INT := -100;
	cMAX_RETRY: INT := 10;
	/// Error Codes
	cERR_NoSIF: INT := -100;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*__________________________________________________________________________________________________
¦	
¦	Vistaprint Schweiz GmbH
¦	CH - 8401 Winterthur
¦
¦	www.vistaprint.ch - info@vistaprint.ch
¦___________________________________________________________________________________________________

Function desription:
Datachannel to send data to Lince Controller
Insert new Job with Method Creat in a Buffer
Send buffer elements if Linecontroller is ready


History:
Version		Date			Author		Comment
---------------------------------------------------------------------------------------------------
0.00.1		26.08.2013		AVM/PRE		Start history


___________________________________________________________________________________________________*)

// Data Channel to Receive Data from Line Controller
// Startup check States
IF (pSIf <> 0) THEN
	IF (intStatemachine < cSTA_SUB_Data_Init) THEN
		intStatemachine := cSTA_SUB_Data_Init;
	END_IF
ELSE
	intError := cERR_NoSIF;
END_IF

// 



CASE intStatemachine OF
	cSTA_SUB_Data_Init:
		IF pSIf^.DtChnToLine.stJobDone.intDataState = cDataState_DONE THEN
			intStatemachine := cSTA_SUB_Data_WaitForJob;
		END_IF
		
	cSTA_SUB_Data_WaitForJob:
		// New data arrived
		IF intPBufferInput <> intPBufferOutput THEN
			IF pSIf^.DtChnToLine.stJobDone.intDataState = cDataState_DONE THEN
				intStatemachine := cSTA_SUB_Data_SendData;
			END_IF
		END_IF
	
	cSTA_SUB_Data_SendData:	
		pSIf^.DtChnToLine.stJobDone.stDecision.uliRFID := aBuffer[intPBufferOutput].stDecision.uliRFID;
		pSIf^.DtChnToLine.stJobDone.stDecision.strID := aBuffer[intPBufferOutput].stDecision.strID;
		pSIf^.DtChnToLine.stJobDone.stDecision.dinTarget := aBuffer[intPBufferOutput].stDecision.dinTarget;
		pSIf^.DtChnToLine.stJobDone.strTimestamp := aBuffer[intPBufferOutput].strTimestamp;
		pSIf^.DtChnToLine.stJobDone.intDataState := cDataState_Ready;
		intStatemachine := cSTA_SUB_Data_Acknowledge;	
		
	cSTA_SUB_Data_Acknowledge:	
		IF pSIf^.DtChnToLine.stJobDone.intDataState = cDataState_DONE THEN
			// Data read from Line Controller
			// Reset Buffer Element
			aBuffer[intPBufferOutput].strTimestamp := '';
			aBuffer[intPBufferOutput].intDataState := 0;
			aBuffer[intPBufferOutput].stDecision.uliRFID := 0;
			aBuffer[intPBufferOutput].stDecision.strID := '';
			aBuffer[intPBufferOutput].stDecision.dinTarget := 0;
			// Increase Read out pointer
			IF intPBufferOutput < cDtChnBuffer THEN
				intPBufferOutput := intPBufferOutput + 1;
			ELSE
				intPBufferOutput := 0;
			END_IF
			intStatemachine := cSTA_SUB_Data_WaitForJob;
			
		ELSIF pSIf^.DtChnToLine.stJobDone.intDataState = cDataState_Retry THEN
			intStatemachine := cSTA_SUB_Data_Retry;
		ELSIF pSIf^.DtChnToLine.stJobDone.intDataState = cDataState_Error THEN
			intStatemachine := cSTA_SUB_Data_Error;
		END_IF
		
	cSTA_SUB_Data_Retry:
			IF intCountRetry < cMAX_RETRY THEN
				intCountRetry := intCountRetry + 1;
				intStatemachine := cSTA_SUB_Data_SendData;
			ELSE
				intStatemachine := cSTA_SUB_Data_Error;
			END_IF

	cSTA_SUB_Data_Error:
		intError := intCountRetry;
		IF bolResetError = TRUE THEN
			intStatemachine := cSTA_SUB_Data_WaitForJob;
		END_IF
			
END_CASE]]></ST>
    </Implementation>
    <Method Name="Creat" Id="{e7a502e3-ee2b-452b-913f-06089279cbb5}">
      <Declaration><![CDATA[METHOD Creat : INT
VAR_INPUT
	In_strTimestamp: STRING(24);
	In_stDecision: T_Decision;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Insert a new Job to send at Line controller
// Only insert the job, if the buffer is not full, next input dosn't cross input and Outputpointer
IF ((intPBufferInput + 1) = intPBufferOutput) OR ((intPBufferInput = cDtChnBuffer) AND (intPBufferOutput = 0)) THEN
	Creat := -99;
ELSE
	aBuffer[intPBufferInput].stDecision.uliRFID := In_stDecision.uliRFID;
	aBuffer[intPBufferInput].stDecision.strID := In_stDecision.strID;
	aBuffer[intPBufferInput].stDecision.dinTarget := In_stDecision.dinTarget;
	aBuffer[intPBufferInput].strTimestamp := In_strTimestamp;
	IF intPBufferInput < cDtChnBuffer THEN
		intPBufferInput := intPBufferInput + 1;
	ELSE
		intPBufferInput := 0;
	END_IF
	Creat := 1;
END_IF]]></ST>
      </Implementation>
      <ObjectProperties />
    </Method>
    <ObjectProperties />
  </POU>
</TcPlcObject>